// file      : bpkg/rep-info.cli
// copyright : Copyright (c) 2014-2016 Code Synthesis Ltd
// license   : MIT; see accompanying LICENSE file

include <bpkg/common.cli>;

"\section=1"
"\name=bpkg-rep-info"
"\summary=print repository information"

namespace bpkg
{
  {
    "<options> <rep>",

    "\h|SYNOPSIS|

     \c{\b{bpkg rep-info} [<options>] <rep>}

     \h|DESCRIPTION|

     The \cb{rep-info} command prints various information about the specified
     repository. By default it print the repository's name and location as the
     first line followed by the list of complement and prerequisite
     repositories and the list of available packages. This default behavior,
     however, can be altered in various ways using options listed below. Note
     that the information is written to \cb{STDOUT}, not \cb{STDERR}."
  }

  class rep_info_options: common_options
  {
    "\h|REP-INFO OPTIONS|"

    bool --name|-n
    {
      "Print the specified repository's name and location."
    }

    bool --repositories|-r
    {
      "Print the list of complement and prerequisite repositories."
    }

    bool --packages|-p
    {
      "Print the list of available packages."
    }

    bool --manifest|-m
    {
      "Instead of printing the information in the human-readable form, dump it
       as manifest(s). Normally you would use this option in combination with
       \cb{--packages|-p} or \cb{--repositories|-r} to only dump one of the
       manifests."
    }
  };
}
