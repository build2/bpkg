# file      : tests/rep-fetch-git-commit.testscript
# copyright : Copyright (c) 2014-2018 Code Synthesis Ltd
# license   : MIT; see accompanying LICENSE file

+git clone "$rep_git/state0/style-basic.git" 2>! &style-basic/***

: unadvertised
:
if ($git_fully_supported || $git_protocol != 'https-smart-unadv')
{
  +git -C ../style-basic log '--pretty=format:%H' --all --grep='README' | \
   set commit

  : no-refname
  :
  : Here we also test that a commit can be specified without leading '@'.
  :
  {
    $clone_root_cfg && $rep_add "$rep/state0/style-basic.git#$commit";

    if ($git_protocol == 'https-dumb')
      warn = "$warn_repo_hist$reason_dumb$warn_dumb"
    elif ($git_protocol != 'https-smart-unadv')
      warn = "$warn_repo_hist$reason_unadv"
    end;

    $* 2>>~"%EOE%"
      %fetching git:.+style-basic#$commit%
      %querying .+style-basic\.git%?
      %fetching from .+style-basic\.git%
      $warn
      1 package\(s\) in 1 repository\(s\)
      EOE
  }

  : refname
  :
  {
    $clone_root_cfg && $rep_add "$rep/state0/style-basic.git#stable@$commit";

    if ($git_protocol == 'https-dumb')
      warn = "$warn_ref_hist$reason_dumb$warn_dumb"
    elif ($git_protocol != 'https-smart-unadv')
      warn = "$warn_ref_hist$reason_unadv"
    end;

    $* 2>>~"%EOE%"
      %fetching git:.+style-basic#stable@$commit%
      %querying .+style-basic\.git%?
      %fetching from .+style-basic\.git%
      $warn
      1 package\(s\) in 1 repository\(s\)
      EOE
  }
}

: advertised
:
{
  +git -C ../style-basic log '--pretty=format:%H' --all --grep='INSTALL' | \
   set commit

  +if ($git_protocol == 'https-dumb')
     warn = "$warn_ref_hist$reason_dumb$warn_dumb"
   end

  : no-refname
  :
  {
    $clone_root_cfg && $rep_add "$rep/state0/style-basic.git#@$commit";

    $* 2>>~"%EOE%"
      %fetching git:.+style-basic#@$commit%
      %querying .+style-basic\.git%?
      %fetching from .+style-basic.\git%
      %querying .+style-basic\.git%?
      $warn
      1 package\(s\) in 1 repository\(s\)
      EOE
  }

  : refname
  :
  {
    $clone_root_cfg && $rep_add "$rep/state0/style-basic.git#stable@$commit";

    $* 2>>~"%EOE%"
      %fetching git:.+style-basic#stable@$commit%
      %querying .+style-basic\.git%?
      %fetching from .+style-basic\.git%
      %querying .+style-basic\.git%?
      $warn
      1 package\(s\) in 1 repository\(s\)
      EOE
  }
}
